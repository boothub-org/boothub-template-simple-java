import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.plugins.signing.Sign

plugins {
    id 'groovy'
    id 'eclipse'
    id 'idea'
    id 'findbugs'
    id 'signing'
    id 'maven-publish'
    id "com.github.ethankhall.semantic-versioning" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.13.0"
    id "com.jfrog.bintray" version "1.7.2"
    id "org.asciidoctor.convert" version "1.5.3"
    id "org.ajoberstar.git-publish" version "0.3.2"
    id "com.github.hierynomus.license" version "0.12.1"
    id "net.saliman.properties" version "1.4.6"
    id 'com.github.jk1.dependency-license-report' version '0.3.8'
}

project.version.with {
    major = boothubTemplateSimpleJavaVersionMajor as int
    minor= boothubTemplateSimpleJavaVersionMinor as int
    patch = boothubTemplateSimpleJavaVersionPatch as int
    if (project.hasProperty('boothubTemplateSimpleJavaVersionLabel')) {
        preRelease = boothubTemplateSimpleJavaVersionLabel
    }
    releaseBuild = Boolean.valueOf(boothubTemplateSimpleJavaReleaseBuild)
}
ext.boothubTemplateSimpleJavaVersion = project.version as String
ext.boothubTemplateSimpleJavaTag = Boolean.valueOf(boothubTemplateSimpleJavaReleaseBuild) ? "v$ext.boothubTemplateSimpleJavaVersion" : 'master'

repositories {
  jcenter()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

def pomConfig = {
    url "https://github.com/boothub-org/boothub-template-simple-java"
    developers {
    }
    scm {
        connection "https://github.com/boothub-org/boothub-template-simple-java.git"
        developerConnection "https://github.com/boothub-org/boothub-template-simple-java.git"
        url "https://github.com/boothub-org/boothub-template-simple-java"
    }
}

ext.groovyVersion = '2.4.12'

group = 'org.boothub'
version = boothubTemplateSimpleJavaVersion

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:all,-syntax,-missing', '-quiet')
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

sourceCompatibility = 1.8

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

[compileGroovy, compileTestGroovy]*.options*.encoding = defaultEncoding
[compileGroovy, compileTestGroovy]*.groovyOptions*.encoding = defaultEncoding

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]
sourceSets.test.resources.srcDirs += 'skeleton'

configurations {
  provided
  compile {transitive = true}
}

license {
    header rootProject.file("license-header.txt")
    skipExistingHeaders true
    ignoreFailures false
    excludes (["**/*.zip", "**/*.yml", "**/*.properties", 'template/**'])
}

signing {
    sign configurations.archives
}

signArchives {
  onlyIf { gradle.taskGraph.allTasks.findAll {task -> isPublishTask(task)} }
}

dependencies {
  compile "org.codehaus.groovy:groovy-all:$groovyVersion"
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

  provided 'com.google.code.findbugs:findbugs-annotations:3.0.1'

  testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
      exclude module: 'groovy-all'
  }
  testCompile 'cglib:cglib-nodep:3.2.4'
  testCompile 'org.objenesis:objenesis:2.4'
  testCompile('com.athaydes:spock-reports:1.2.13') { transitive = false}

  testCompile('ch.qos.logback:logback-classic:1.1.7')

  testCompile ("org.boothub:boothub-core:$boothubVersion")
  testCompile ("org.boothub:boothub-gradle-test:$boothubVersion")
  testCompile gradleTestKit()
}

sourceSets.main.compileClasspath += configurations.provided
findbugs {
    effort = "max"
    excludeFilter = file("$rootProject.projectDir/findbugsExclude.xml")
}
jar {
    manifest {
        attributes 'Implementation-Title': "${project.archivesBaseName}",
                'Implementation-Version': boothubTemplateSimpleJavaVersion
    }

    from ("${projectDir}/src/main/java") {
        include('**/*.fxml')
        include('**/*.css')
        include('**/*.png')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task groovydocJar(type: Jar, dependsOn: classes) {
    classifier "javadoc"
    from groovydoc
}

artifacts {
    (System.getenv("TRAVIS") == "true") ? archives(sourcesJar) : archives(sourcesJar, groovydocJar)
}

publishing {
    publications {
        "boothub-template-simple-java"(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
            artifact groovydocJar { classifier "javadoc" }
            groupId 'org.boothub'
            artifactId project.name
            version boothubTemplateSimpleJavaVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('name', "Module $project.name")
                root.appendNode('description', "The $project.name artifact")
                root.children().last() + pomConfig

                // We cannot configure the licenses in pomConfig due to a name conflict with the 'license' extension defined by the com.github.hierynomus.license plugin
                def licenseNode = root.appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode('distribution', 'repo')
            }
        }
    }
}

task "create-dirs" {
    doLast {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.groovy.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

eclipse {
    classpath {
        file {
            whenMerged {
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new org.gradle.plugins.ide.eclipse.model.AccessRule('0', 'javafx/**'))
            }
        }
        plusConfigurations << configurations.provided
    }
    project {
        file.beforeMerged { project ->
            project.natures.clear()
        }
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

def gitIgnore = file(".gitignore").readLines()
def gitIgnoreDirs = gitIgnore*.trim().findAll { !it.startsWith("#") && it.endsWith("/") }
asciidoctor {
  sourceDir = file("doc")
  sources {include 'index.adoc'}
  logDocuments = true
  attributes 'source-highlighter': 'coderay',
             'coderay-linenums-mode': 'table',
             icons: 'font',
             imagesdir: 'img',
             'data-uri': '',
             linkattrs: true,
             linkcss: true,
             'git-tag': boothubTemplateSimpleJavaTag,
             'blob-root': "https://github.com/boothub-org/boothub-template-simple-java/blob/$boothubTemplateSimpleJavaTag"
}

gitPublish {
    System.properties['org.ajoberstar.grgit.auth.username'] = System.getenv('GH_PAGES_ACCESS_TOKEN') ?: ''
    System.properties['org.ajoberstar.grgit.auth.password'] = ''

    repoUri = 'https://github.com/boothub-org/boothub-template-simple-java.git'
    branch = 'gh-pages'

    contents {
        def pgType = project.hasProperty('ghPageType') ? ghPageType : 'latest'
        if(pgType == 'init') {
            from file("ghpages")
        } else {
            from file("${asciidoctor.outputDir.path}/html5")
            from file("build/docs")
        }
        def docDir = Boolean.valueOf(boothubTemplateSimpleJavaReleaseBuild) ? 'releases' : 'snapshots'
        if(pgType == 'init') {
            into "."
        } else if(pgType == 'version') {
            gitPublishPush.enabled = (docDir != 'snapshots')
            into "$docDir/$boothubTemplateSimpleJavaVersion"

            preserve {
                include '**'
                exclude "$docDir/$boothubTemplateSimpleJavaVersion"
            }
        } else {
            into "$docDir/latest"

            preserve {
                include '**'
                exclude "$docDir/latest"
            }
        }
    }
}



artifacts {
    (System.getenv("TRAVIS") == "true") ? archives(sourcesJar) : archives(sourcesJar, groovydocJar)
}



configureGroovydoc(groovydoc)

def configureGroovydoc(task) {
  configure(task) {
    docTitle "boothub-template-simple-java Documentation ($boothubTemplateSimpleJavaVersion)"
    windowTitle "boothub-template-simple-java Documentation ($boothubTemplateSimpleJavaVersion)"
    destinationDir file("build/docs/groovydoc")
    if(project.configurations.hasProperty('provided')) {
        classpath += project.configurations.provided
    }
    link "http://docs.oracle.com/javase/8/docs/api/", 'java'
    link "http://docs.groovy-lang.org/docs/groovy-$groovyVersion/html/gapi/", "groovy"
  }
}

def readFromConsole(prompt) {
    readFromConsole('', prompt)
}
def readFromConsole(header, prompt) {
    readFromConsole(header, prompt, false)
}
def readPasswordFromConsole(prompt) {
    readPasswordFromConsole(prompt, true)
}
def readPasswordFromConsole(header, prompt) {
    readFromConsole(header, prompt, true)
}
def readFromConsole(header, prompt, isPassword) {
    def console = System.console()
    if (console) {
        if(header) {
            console.printf "\n\n# $header\n"
        }
        String value = null;
        while(!value?.trim()) {
            def val = console."read${isPassword ? 'Password' : 'Line'}"("> $prompt: ")
            if(val) {
                value = new String(val)
            }
        }
        return value
    } else {
      throw new GradleException("Cannot get console. Try running gradle with the '--no-daemon' option")
    }
}

def isPublishTask(task) {
  task.name.startsWith('publish')
}

gradle.taskGraph.whenReady { taskGraph ->
    if (gradle.taskGraph.allTasks.findAll {task -> isPublishTask(task)}) {
        def password = readPasswordFromConsole('Please enter your signing credentials', 'PGP Private Key Password')
        allprojects { ext."signing.keyId" = signingKeyId }
        allprojects { ext."signing.secretKeyRingFile" = signingSecretKeyRingFile }
        allprojects { ext."signing.password" = password }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : "unknownUser"
    key =  project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : "unknownKey"
    publications = ['boothub-template-simple-java']
    pkg {
        repo = 'maven'
        name = 'boothub-template-simple-java'
        userOrg = 'boothub-org'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/boothub-org/boothub-template-simple-java.git'

        version {
                name = boothubTemplateSimpleJavaVersion
                desc = "boothub-template-simple-java $boothubTemplateSimpleJavaVersion"
                released  = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
                vcsTag = boothubTemplateSimpleJavaVersion
                gpg {
                    sign = true
                }
        }
    }
}
